rule "Gas calc"
	when 
		Time cron "0 0/1 * * * ?"
	then
		val Number gcons = WB_GasCounter.deltaSince(now.minusMinutes(1))
		val Number gcons_today = WB_GasCounter.deltaSince(now.withTimeAtStartOfDay())
		val Number gcons_yest = ((WB_GasCounter.historicState(now.withTimeAtStartOfDay(),"rrd4j")).state as DecimalType).floatValue - ((WB_GasCounter.historicState(now.withTimeAtStartOfDay().minusDays(1),"rrd4j")).state as DecimalType).floatValue;

		if (gcons!=null) Gas_Cons.postUpdate(gcons)
		if (gcons_today!=null) Gas_ConsMN.postUpdate(gcons_today)
		if (gcons_yest!=null) Gas_ConsD.postUpdate(gcons_yest)

end

rule "Power calc"
	when 
		Time cron "0 0 0 * * ?"
	then
val Number pcons = Power_Energy.deltaSince(now.minusDays(1))
if (pcons!=null) Power_Cons.postUpdate(pcons)
end

rule "Power calcMN"
	when 
		Time cron "0 0/1 * * * ?"
	then
val Number pconsmn = Power_Energy.deltaSince(now.withTimeAtStartOfDay())
if (pconsmn!=null) Power_ConsMN.postUpdate(pconsmn)
end

rule "Send counters"
	when 
		Time cron "0 45 9 1 * ?"
	then
		val first = new DateTime(now.withTimeAtStartOfDay.withDayOfMonth(1));
		val second = new DateTime(now.withTimeAtStartOfDay.minusMonths(1).withDayOfMonth(1));
		val Number gcount = ((WB_GasCounter.historicState(first,"rrd4j")).state as DecimalType).floatValue;
		val Number g_old_count = ((WB_GasCounter.historicState(second,"rrd4j")).state as DecimalType).floatValue;
		val Number pcount = ((Power_Energy.historicState(first,"rrd4j")).state as DecimalType).floatValue;
		val Number p_old_count = ((Power_Energy.historicState(second,"rrd4j")).state as DecimalType).floatValue;

		var String message1 = "Показники лічильника газу\n\n";
		message1 += String::format("Показник станом на 01.%1$02d.%2$02d: %3$.2f m3\n", second.getMonthOfYear, second.getYear, (g_old_count/100));	
		message1 += String::format("Показник станом на 01.%1$02d.%2$02d: %3$.2f m3\n", first.getMonthOfYear, first.getYear, (gcount/100));	
		message1 += String::format("Спожито за місяць: %1$.2f m3", ((gcount-g_old_count)/100));	

		var String message2 = "Показники лічильника ел.енергії\n\n";
		message2 += String::format("Показник станом на 01.%1$02d.%2$02d: %3$.2f kWh\n", second.getMonthOfYear, second.getYear, p_old_count);	
		message2 += String::format("Показник станом на 01.%1$02d.%2$02d: %3$.2f kWh\n", first.getMonthOfYear, first.getYear, pcount);	
		message2 += String::format("Спожито за місяць: %1$.2f kWh", (pcount-p_old_count));	
		val mailActions = getActions("mail","mail:smtp:327b0d61")
		mailActions.sendMail("sv_h@ukr.net", "Показники лічильників", message1 + "\n\n\n" + message2);
		
end

rule "get_8266_data"
when
	Item TmpString changed
then
	val newtemp = transform("REGEX", ".*?ppm:T(.*?)C:H.*", TmpString.state.toString)
	val newhum = transform("REGEX", ".*?C:H(.*?)%:d.*", TmpString.state.toString)
	val newco2 = transform("REGEX", ".*?C(.*?)ppm:T.*", TmpString.state.toString)

	if (newco2!=null) RoomCO2.postUpdate(newco2)
	if (newhum!=null) RoomHum.postUpdate(newhum)
	if (newtemp!=null) RoomTemp.postUpdate(newtemp)

end
